#### Sonido y Pitch editado
Black
Line width: 1
Speckle size: 1
sonido = selected("Sound")
tono_nat = selected("Pitch")
sonido$ = selected$("Sound")
sexo$ = mid$(sonido$,5)

if sexo$ = "h"
	techo = 500
elsif sexo$ ="m"
	techo = 700
endif

### Normaliza, remuestrea y filtra
select sonido
dur = Get total duration
dur$ = fixed$(dur,3)

Scale peak: 0.99
fm= Get sampling frequency

if fm = 44100 or fm = 22050
	Resample: 16000, 50
endif

Filter (stop Hann band): 0, 70, 100

### Asigna nombre "sonido" a la variable.
sonido = selected("Sound")

### Alisa el tono
select tono_nat
tono_smo = Smooth: 10

### Crear el estilizado
select tono_nat
plus sonido
To Manipulation
tono_man = Extract pitch tier
Stylize: 2, "Semitones"
n_puntos_tono_man = Get number of points
n_puntos_tono_man = n_puntos_tono_man -1

### Dibuja el oscilograma
Select inner viewport: 0.5, 8, 0.5, 1.5
Axes: 0, dur, -1, 1
Paint rectangle: "{0.8,0.8,0.8}", 0, dur, -1, 1

select  sonido
Blue

Draw: 0, 0, 0, 0, "no", "Curve"
Draw inner box
# Text top: "no", dur$ + " segs"


#### Dibujar tono ####
Select inner viewport: 0.5, 8, 1.5, 3.5
Axes: 0, dur, 0, techo
Paint rectangle: "{0.9,0.9,0.9}", 0, dur, 0, techo

Speckle size: 1.5
Grey
select tono_nat
Speckle: 0, 0, 0, techo, "no"

Speckle size: 0.5
Red
select tono_smo
Speckle: 0, 0, 0, techo, "no"

Blue
Speckle size: 0.25
#select tono_man
#Draw: 0, 0, 0, techo, "no", "lines and speckles"

select tono_man

for i to n_puntos_tono_man
	tiempo_i_i = Get time from index: i
	tiempo_i_f = Get time from index: i + 1
	hz_i_i = Get value at time: tiempo_i_i
	hz_i_f = Get value at time: tiempo_i_f
	Draw line: tiempo_i_i, hz_i_i, tiempo_i_f, hz_i_f
endfor

Draw inner box
Marks right every: 1, 50, "no", "yes", "yes"
Marks right every: 1, 100, "yes", "no", "no"

Select outer viewport: 0.5, 8.3, 0.2, 3.6

Save as 600-dpi PNG file: sonido$ +".png"

Speckle size: 1
Erase all
