# Script para Praat
# Laboratorio de Fonética USACH
# 2017
# Proyecto: Mapa prosódico de Chile
#
# Este script es software libre: puede ser distribuido
# y/o modificado bajo los términos de la licencia
# GNU General Public License publicada por la Free Software
# Foundation, ya sea en su versión 3 o (según se prefiera)
# cualquier versión posterior.
#
# Este script se distribuye con la intención de que sea útil,
# pero SIN GARANTÍA ALGUNA; ni siquiera con la garantía
# implícita de COMERCIABILIDAD o de PROPIEDAD PARA UN FIN
# PARTICULAR. Más detalles pueden encontrarse en el texto de
# la GNU General Public License.
#
# El script debería haber sido distribuido con una copia de la
# GNU General Public License. Si no es el caso, este puede encontrarse en
# <http://www.gnu.org/licenses/>.
#
# Copyright 2017 Domingo Román
#
#

# Selecciona el directorio

directorio$ = chooseDirectory$: "Elige la carpeta para los gráficos"

#### Sonido y Pitch editado
Erase all
Black
Line width: 1
Speckle size: 1

select all
sonido = selected("Sound")
tono_nat = selected("Pitch")
sonido$ = selected$("Sound")
tg = selected("TextGrid")

sexo$ = mid$(sonido$,4)

tono$ = "_tono"
if sexo$ = "h"
	techo = 500
elsif sexo$ ="m"
	techo = 700
endif

### Normaliza, remuestrea y filtra
select sonido
dur = Get total duration
dur$ = fixed$(dur,3)

Subtract mean

fm= Get sampling frequency

if fm = 44100 or fm = 22050 or fm = 16000
	remuestr = Resample: 16000, 50
endif

Filter (stop Hann band): 0, 70, 100

### Asigna nombre "sonido" al sonido filtrado.
sonido_a = selected("Sound")
Scale peak: 0.99

### Alisa el tono
select tono_nat
tono_smo = Smooth: 10

### Crear el estilizado
select tono_nat
plus sonido_a
manip = To Manipulation
tono_man = Extract pitch tier
Stylize: 2, "Semitones"
n_puntos_tono_man = Get number of points
n_puntos_tono_man = n_puntos_tono_man -1

### Dibuja el oscilograma
Select inner viewport: 0.5, 8, 0.5, 1.5
Axes: 0, dur, -1, 1
Paint rectangle: "{0.8,0.8,0.8}", 0, dur, -1, 1

select  sonido_a
Blue

Draw: 0, 0, -1, 1, "no", "Curve"
Draw inner box
Black
Text special: dur, "Right", 1, "Bottom", "Times", 8, "0", "("+dur$+" segs)"


#### Dibujar tono ####
Select inner viewport: 0.5, 8, 1.5, 3.5
Axes: 0, dur, 0, techo
Paint rectangle: "{0.9,0.9,0.9}", 0, dur, 0, techo

Speckle size: 1.5
Grey
select tono_nat
Speckle: 0, 0, 0, techo, "no"

Speckle size: 0.5
Red
select tono_smo
Speckle: 0, 0, 0, techo, "no"

tonosuav = To Sound (hum)

Save as WAV file: directorio$+"/"+sonido$+tono$+".wav"

Blue
Speckle size: 0.25
#select tono_man
#Draw: 0, 0, 0, techo, "no", "lines and speckles"

select tono_man

for i to n_puntos_tono_man
	tiempo_i_i = Get time from index: i
	tiempo_i_f = Get time from index: i + 1
	hz_i_i = Get value at time: tiempo_i_i
	hz_i_f = Get value at time: tiempo_i_f
	Draw line: tiempo_i_i, hz_i_i, tiempo_i_f, hz_i_f
endfor

Draw inner box
Marks right every: 1, 50, "no", "yes", "yes"
Marks right every: 1, 100, "yes", "no", "no"

# Guarda los gráfico
Select outer viewport: 0.5, 8.3, 0.2, 3.6
Save as 600-dpi PNG file: directorio$+"/"+sonido$+".png"

# Guarda audio submuestreado
select sonido_a
Save as WAV file: directorio$+"/"+sonido$+".wav"

# Muestra y Borra el gráfico
pause
Erase all

# Remueve los objetos de PraatObjects
select sonido_a
plus remuestr
plus sonido
plus tono_man
plus tono_smo
plus tono_nat
plus manip
plus tonosuav
plus tg
Remove
